wrapper {
    gradleVersion = '7.6.2'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

group = pGroup
version = pVersion
description = pDescription

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (project.hasProperty("topobyte")) {
    apply from: 'file:///' + System.getProperty("user.home") \
        + '/.gradle/topobyte.gradle'
}

buildscript {
	dependencies {
		classpath 'junit:junit:4.12'
		classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
	}
	repositories {
		mavenCentral()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation gradleApi()
	implementation localGroovy()

    implementation 'commons-io:commons-io:2.6'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

pluginBundle {
  website = 'http://www.topobyte.de'
  vcsUrl = 'https://github.com/sebkur/gradle-eclipse-pde-plugin'
  description = 'Topobyte Eclipse PDE plugin'

  plugins {
    eclipsePdePlugin {
      id = 'de.topobyte.eclipse-pde-gradle-plugin'
      displayName = 'Topobyte Eclipse PDE plugin'
    }
  }
}

artifacts {
    archives sourcesJar
}

def pomInfo = new XmlSlurper().parse(new File('info.pom'));

publishing {
    repositories {
        maven {
            url = maven_path_topobyte_temp
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "$pomInfo.name"
                description = "$pomInfo.description"
                url = "$pomInfo.url"
                licenses {
                    license {
                        name = "$pomInfo.licenseName"
                        url = "$pomInfo.licenseUrl"
                        distribution = "$pomInfo.licenseDistribution"
                    }
                }
            }
        }
    }
}
